[Setup]
AppName=@PACKAGE_NAME@
AppVersion=@PACKAGE_VERSION@
AppVerName=@PACKAGE_STRING@
AppPublisher=Trific soft.
AppPublisherURL=@PACKAGE_URL@
AppCopyright=Copyright (C) 2003,2004 David Necas (Yeti), Petr Klapetek
DefaultDirName={pf}\@PACKAGE_NAME@
DefaultGroupName=@PACKAGE_NAME@
DisableStartupPrompt=yes
WindowShowCaption=yes
WindowVisible=no
LicenseFile=COPYING.wri
Compression=bzip/9
SourceDir=.
OutputDir=.
OutputBaseFilename=@PACKAGE_NAME@-@PACKAGE_VERSION@
; I would like to, but don't know how :-(
;ChangesAssociations=yes

[Files]
Source: "inst\*.*"; DestDir: "{app}"
Source: "inst\pixmaps\*.*"; DestDir: "{app}\pixmaps"
Source: "inst\modules\*.*"; DestDir: "{app}\modules"
Source: "inst\modules\file\*.*"; DestDir: "{app}\modules\file"
Source: "inst\modules\graph\*.*"; DestDir: "{app}\modules\graph"
Source: "inst\modules\process\*.*"; DestDir: "{app}\modules\process"
Source: "inst\modules\tool\*.*"; DestDir: "{app}\modules\tool"
Source: "inst\lib\*.*"; DestDir: "{app}\lib"
Source: "inst\include\libgwyddion\*.*"; DestDir: "{app}\include\libgwyddion"
Source: "inst\include\libprocess\*.*"; DestDir: "{app}\include\libprocess"
Source: "inst\include\libdraw\*.*"; DestDir: "{app}\include\libdraw"
Source: "inst\include\libgwydgets\*.*"; DestDir: "{app}\include\libgwydgets"
Source: "inst\include\libgwymodule\*.*"; DestDir: "{app}\include\libgwymodule"
Source: "inst\include\libgwyapp\*.*"; DestDir: "{app}\include\libgwyapp"
Source: "COPYING.wri"; DestDir: "{app}"
;Source: "README.wri"; DestDir: "{app}"; Flags: isreadme

[Icons]
Name: "{group}\@PACKAGE_NAME@"; Filename: "{app}\@PACKAGE_TARNAME@.exe"
Name: "{group}\@PACKAGE_NAME@ Website"; Filename: "@PACKAGE_URL@"
Name: "{group}\@PACKAGE_NAME@ Development Website"; Filename: "http://trific.ath.cx/software/@PACKAGE_TARNAME@/"
Name: "{group}\Uninstall @PACKAGE_NAME@"; Filename: "{uninstallexe}"
Name: "{userdesktop}\@PACKAGE_NAME@"; Filename: "{app}\@PACKAGE_TARNAME@.exe"

[Registry]
; This adds the GTK+ libraries to @PACKAGE_TARNAME@.exe's path
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\@PACKAGE_TARNAME@.exe"; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\@PACKAGE_TARNAME@.exe"; ValueType: string; ValueData: "{app}\@PACKAGE_TARNAME@.exe"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\@PACKAGE_TARNAME@.exe"; ValueType: string; ValueName: "Path"; ValueData: "{app};{code:GetGtkPath}\lib"; Flags: uninsdeletevalue

[Code]
var
  Exists: Boolean;
  GtkPath: String;

function GetGtkInstalled (): Boolean;
begin
  Exists := RegQueryStringValue (HKLM, 'Software\GTK\2.0', 'Path', GtkPath);
  if not Exists then begin
    Exists := RegQueryStringValue (HKCU, 'Software\GTK\2.0', 'Path', GtkPath);
  end;
   Result := Exists
end;

function GetGtkPath (S: String): String;
begin
    Result := GtkPath;
end;

function InitializeSetup(): Boolean;
begin
  Result := GetGtkInstalled ();
  if not Result then begin
    MsgBox ('Please install the GTK+ 2.0 Runtime Environment before installing @PACKAGE_NAME@.  You can obtain GTK+ from http://www.dropline.net/gtk.', mbError, MB_OK);
  end;
end;

